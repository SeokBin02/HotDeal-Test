plugins {
    id 'java'
    id 'org.springframework.boot' version '2.7.12'
    id 'io.spring.dependency-management' version '1.0.15.RELEASE'
    id 'jacoco'
}

group = 'Challenge18'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

jacoco {
    toolVersion = "0.8.8"
}

repositories {
    mavenCentral()
}

dependencies {
    //JWT 추가하기
    compileOnly group: 'io.jsonwebtoken', name: 'jjwt-api', version: '0.11.2'
    runtimeOnly group: 'io.jsonwebtoken', name: 'jjwt-impl', version: '0.11.2'
    runtimeOnly group: 'io.jsonwebtoken', name: 'jjwt-jackson', version: '0.11.2'

    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    runtimeOnly 'com.h2database:h2'
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'com.mysql:mysql-connector-j'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter'

    // QueryDSL
    implementation 'com.querydsl:querydsl-jpa' // QueryDSL JPA 사용
    implementation 'com.querydsl:querydsl-apt' // QClass 생성
    annotationProcessor "com.querydsl:querydsl-apt:${dependencyManagement.importedProperties['querydsl.version']}:jpa" // @Entity를 탐색해 QClass 생성
    annotationProcessor 'jakarta.persistence:jakarta.persistence-api' // QFile을 찾지 못하는 에러인 NoClassDefFountError 에 대응
    annotationProcessor 'jakarta.annotation:jakarta.annotation-api'

    // Redis
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.redisson:redisson-spring-boot-starter:3.17.4'
}

// gradle build시 QFile도 함께 build하기 위해 저장 위치 알려줌
def querydslSrcDir = 'src/main/generated'
sourceSets {
    main {
        java {
            srcDirs += [ querydslSrcDir ]
        }
    }
}

// 자바 9 이상의 버전에서도 QFile의 Generated를 사용할 수 있게 설정
compileJava {
    options.compilerArgs << '-Aquerydsl.generatedAnnotationClass=javax.annotation.Generated'
}

// QFile 저장 위치 설정
tasks.withType(JavaCompile) {
    options.generatedSourceOutputDirectory = file(querydslSrcDir)
}

// build clean 시, 생성된 QFile 모두 삭제
clean {
    delete file(querydslSrcDir)
}

// plain.jar 생성 비활성화
jar {
    enabled = false
}

// build시 app.jar로 생성
bootJar{
    archivesBaseName = 'app'
    archiveFileName = 'app.jar'
    archiveVersion = "0.0.0"
}

tasks.named('test') {
    useJUnitPlatform()
}

jacocoTestReport {
    reports {
        xml.required = false
        csv.required = false
        html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            enabled = true
            element = 'CLASS'

            limit {
                counter = 'LINE'
                value = 'TOTALCOUNT'
                maximum = 0.7
            }
        }
    }
}